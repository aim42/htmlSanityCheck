= HSC Gradle Plugin
:doctype: book
include::../asciidoctor-config.ad[]

image:https://img.shields.io/gradle-plugin-portal/v/org.aim42.htmlSanityCheck[link=https://plugins.gradle.org/search?term=org.aim42.htmlSanityCheck]
image:https://img.shields.io/maven-central/v/org.aim42.htmlSanityCheck/org.aim42.htmlSanityCheck.gradle.plugin[link=https://central.sonatype.com/artifact/org.aim42.htmlSanityCheck/org.aim42.htmlSanityCheck.gradle.plugin]

ifdef::env-github[]
:imagesdir: ../src/docs/images
endif::env-github[]
ifndef::xrefToManual[:xrefToManual: ../README.adoc]
:gradleProperties: link:../gradle.properties[]
ifdef::jbake-type[:gradleProperties: {project-url}/blob/develop/gradle.properties[]]

The {gradle-url}[Gradle] plugin of HTML Sanity Check (xref:{xrefToManual}[HSC]) enables to check generated or native HTML documentation from the Gradle build.

[[sec:installation]]
== Installation (Gradle Plugin)

=== Default Installation

Use the following snippet inside a Gradle build file:

.build.gradle
[source,groovy,subs="attributes+"]
----
plugins {
    id 'org.aim42.{project}' version '{hsc-version}' // <1>
}
----

<1> Checkout <<box:current-version,current version>>

=== Legacy Installation

In the case of https://docs.gradle.org/current/userguide/plugins.html#sec:old_plugin_application[legacy plugin usage]

.build.gradle
[source,groovy,subs="attributes+"]
----
buildscript {
    repositories {
        // maven { url "{jitpack-url}" } // <1>
        mavenCentral() // <2>
        gradlePluginPortal() // <3>
    }

    dependencies {
        classpath ('gradle.plugin.org.aim42:{project}:{hsc-version}') // <4>
    }
}

apply plugin: 'org.aim42.{project}'
----

<1> In case you would like to use a development version (or even branch), check out <<sec:development-versions,development versions>>.
<2> Beginning with version `2.x` all releases will be published to https://central.sonatype.com/artifact/org.aim42.htmlSanityCheck/org.aim42.htmlSanityCheck.gradle.plugin[Maven Central].
<3> The https://plugins.gradle.org[Gradle Plugin Portal] contains https://plugins.gradle.org/plugin/org.aim42.htmlSanityCheck[most versions] or will redirect downloads of newer versions to Maven Central.
<4> Check out <<box:current-version,the current version>>.

[[box:current-version]]
[IMPORTANT]
.Latest (development) versions
====
* https://github.com/aim42/htmlSanityCheck/blob/main/gradle.properties[`gradle.properties` on `main` branch] defines the latest https://github.com/aim42/htmlSanityCheck/releases[release version].
* {gradleProperties} defines the current (development) version.
+
[source]
.Development version
----
include::../gradle.properties[tag=version]
----
====

[[sec:usage]]
== Usage

The plugin adds a new task named `htmlSanityCheck`.

This task exposes a few properties as part of its configuration:

[horizontal]
`sourceDir` (mandatory):: Directory containing the HTML files to check.
+
Type: Directory.

`sourceDocuments` (optional):: An override to process several source files, which may be a subset of all files available in `+{sourceDir}+`.
+
Type: `org.gradle.api.file.FileCollection`.
+
Default: All files in `+{sourceDir}+` whose names end with `.html`.

`checkingResultsDir` (optional):: Directory where the checking results written to.
+
Type: Directory.
+
Default: `+{buildDir}+/reports/htmlSanityCheck/`

`junitResultsDir` (optional):: Directory where HSC writes the results in JUnit XML format.
Many tools support https://github.com/testmoapp/junitxml[JUnit XML] format, including CI environments.
+
Type: Directory.
+
Default: `+{buildDir}+/test-results/htmlchecks/`

`failOnErrors` (optional):: Fail the build if any checked of the pages contains an error.
+
Type: Boolean.
+
Default: `false`.

`httpConnectionTimeout` (optional):: Timeout for http requests in ms.
+
Type: Integer.
+
Default: `5000`.

`ignoreLocalHost` (optional):: Ignore localhost as hostname.
+
Type: Boolean.
+
Default: `false`.

`ignoreIPAddresses` (optional):: Ignore IP addresses as hostname.
+
Type: Boolean.
+
Default: `false`.

`retries` (optional):: Retry on failed requests (e.g., network issues).
+
*Note*: Currently only used for `UnknownHostException`s (cf.
{project-issues}/270[#270]).
+
Type: Integer.
+
Default: `0`.

`checkerClasses` (optional):: The set of checker classes to be executed.
+
Type: List.
+
Default: All available checker classes.
+
[source,groovy]
.Checker Classes
----
include::../htmlSanityCheck-core/src/main/java/org/aim42/htmlsanitycheck/check/AllCheckers.java[tag=checker-classes,indent=0]
----

`httpWarningCodes` (optional):: Additional HTTP response codes treated as warning.
+
Type: List.
+
Default:
+
[source,groovy]
----
include::../htmlSanityCheck-core/src/main/java/org/aim42/htmlsanitycheck/tools/Web.java[tag=HTTP_WARNING_CODES,indent=0]
// Redirects included
include::../htmlSanityCheck-core/src/main/java/org/aim42/htmlsanitycheck/tools/Web.java[tag=HTTP_REDIRECT_CODES,indent=0]
----
+

[NOTE]
.HTTP Redirects
====
Note that HSC treats HTTP redirects as a warning to make the user aware of the correct or new location (cf.
{project-issues}/244[Issue 244]).
Some HSC reports often contain the respective location.
====

`httpErrorCodes` (optional):: Additional HTTP response codes treated as error.
+
Type: List.
+
Default:
+
[source,groovy]
----
include::../htmlSanityCheck-core/src/main/java/org/aim42/htmlsanitycheck/tools/Web.java[tag=HTTP_ERROR_CODES,indent=0]
----

`httpSuccessCodes` (optional):: Additional HTTP response codes treated as success.
+
Type: List.
+
Default:
+
[source,groovy]
----
include::../htmlSanityCheck-core/src/main/java/org/aim42/htmlsanitycheck/tools/Web.java[tag=HTTP_SUCCESS_CODES,indent=0]
----

[[sidebar:http-response-codes]]
.HTTP response code handling
****
The lists shown above are the default HTTP response codes handled by HSC.
The mentioned configurations effectively move the configured codes around, i.e., if you add `308` to `httpErrorCodes` it is automatically removed from its default list (`httpWarningCodes`).
****

[[sec:examples]]
== Examples

=== Small Example

`build.gradle`

[source,groovy]
----
apply plugin: 'org.aim42.htmlSanityCheck'

htmlSanityCheck {
    sourceDir = file( "$buildDir/docs" )

    // where to put results of sanityChecks...
    checkingResultsDir = file( "$buildDir/report/htmlchecks" )

    // fail build on errors?
    failOnErrors = true

}
----

=== Extended example

.`build.gradle`
[source,groovy,subs='attributes']
----

import org.aim42.htmlsanitycheck.check.*

buildscript {
    repositories {
        mavenCentral()
        // This is only necessary for older releases (< 2.00)
        gradlePluginPortal()
    }
}

plugins {
    id 'org.aim42.htmlsanitycheck' version '{hsc-version}'
    id 'org.asciidoctor.convert' version '1.5.8'
}

// ==== path definitions =====
// ===========================

// location of AsciiDoc files
def asciidocSrcPath = "$projectDir/src/asciidoc"

// location of images used in AsciiDoc documentation
def srcImagesPath = "$asciidocSrcPath/images"

// results of asciidoc compilation (HTML)
// (input for htmlSanityCheck)
// this is the default path for asciidoc-gradle-convert
def htmlOutputPath = "$buildDir/asciidoc/html5"

// images used by generated html
def targetImagesPath =   htmlOutputPath + "/images"

// where HTMLSanityCheck checking results ares stored
def checkingResultsPath = "$buildDir/report/htmlchecks"


apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    sourceDir = new File( asciidocSrcPath )

    options backends: ['html5'],
            doctype: 'book',
            icons: 'font',
            sectlink: true,
            sectanchors: true

    resources {
        from( srcImagesPath )
        into targetImagesPath
    }
}

apply plugin: 'org.aim42.htmlSanityCheck'

htmlSanityCheck {
    // ensure asciidoctor->html runs first
    // and images are copied to build directory

    dependsOn asciidoctor

    sourceDir = new File( htmlOutputPath )

    // files to check, specified as a file tree with filtering
    sourceDocuments = fileTree(sourceDir) {
        include "many-errors.html", "no-errors.html"
    }

    // where to put results of sanityChecks...
    checkingResultsDir = new File( checkingResultsPath )

    // fail build on errors
    failOnErrors = true

   // http connection timeout in milliseconds
    httpConnectionTimeout = 1000

    // which statuscodes shall be interpreted as warning, error or success
    // defaults to standard
    httpWarningCodes = [401]
    // httpErrorCodes
    // httpSuccessCodes

    // only execute a subset of all available checks
    // available checker:
    //   * BrokenCrossReferencesChecker
    //   * BrokenHttpLinksChecker
    //   * DuplicateIdChecker
    //   * ImageMapChecker
    //   * MissingAltInImageTagsChecker
    //   * MissingImageFilesChecker
    //   * MissingLocalResourcesChecker
    checkerClasses = [DuplicateIdChecker, MissingImageFilesChecker]

}

----

== Compatibility

We test the Plugin with the following Gradle versions:

[source,groovy]
.Tested Gradle versions
----
include::src/test/groovy/org/aim42/htmlsanitycheck/gradle/HtmlSanityCheckTaskFunctionalSpec.groovy[tag=tested-gradle-versions,indent=0]
----

[NOTE]
====
We test the full range of Gradle versions only in CI (GitHub action) as it is time-consuming.
The local test only tests the latest Gradle version:

[source,groovy]
.Restricting versions to the latest versions locally
----
include::src/test/groovy/org/aim42/htmlsanitycheck/gradle/HtmlSanityCheckTaskFunctionalSpec.groovy[tag=restrict-gradle-versions-locally,indent=0]
----
====

[[sec:development-versions]]
== Development versions

In case you want to use a current development (or arbitrary branch or tag) version from GitHub,
add the following to your `settings.gradle`:

.settings.gradle
[source,groovy,subs="attributes+"]
----
pluginManagement {
    repositories {
        maven { url "{jitpack-url}" }
    }
}
----

Then you can use a respective version in your `build.gradle`:

.build.gradle
[source,groovy,subs="attributes+"]
----
plugins {
    id 'org.aim42.{project}' version 'develop-SNAPSHOT'
}
----

[NOTE]
.JitPack builds
====
Building the desired version for the first time (or after some cache expiry at {jitpack-url}[JitPack]), you may experience a timeout.

You can look up the https://jitpack.io/#org.aim42/htmlSanityCheck[current build state]:

image::jitpack-branch-screenshot.png[]
====




