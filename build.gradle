buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'java'

    alias(libs.plugins.gradle.versions)
    alias(libs.plugins.sonar)

    alias(libs.plugins.jreleaser)
}

allprojects {
    group = group
    version = htmlSanityCheckVersion

    repositories {
        mavenLocal()
        mavenCentral()
    }

    tasks.register("info") {
        doLast
                {
                    println "project.name         : " + project.name
                    println "version              : " + version
                    println "project.version      : " + project.version
                    println "project.path         : " + project.path
                    println "projectDir           : " + projectDir
                    println "groupId              : " + project.group
                    println "targetCompatibility  : " + java.targetCompatibility
                    println "OS                   : " + System.properties["os.name"]
                    println "Java VM Name         : " + System.properties["java.vm.name"]
                    println "Java VM Vendor       : " + System.properties["java.vm.vendor"]
                    println "Java VM Version      : " + System.properties["java.vm.version"]
                    println "=" * 80
                }
    }
}

ext.urls = [
        website            : "https://aim42.github.io/htmlSanityCheck/",
        issueTracker       : 'https://github.com/aim42/htmlSanityCheck/issues',

        scm                : 'https://github.com/aim42/htmlSanityCheck.git',
        connection         : 'scm:git:git://github.com/aim42/htmlSanityCheck.git',
        developerConnection: 'scm:git:ssh://github.com/aim42/htmlSanityCheck.git'
]

// tag::mavenBuildRepo[]
File baseBuildDir = file("${project.rootDir}/${Project.DEFAULT_BUILD_DIR_NAME}")
File mavenBuildRepo = new File(baseBuildDir, "maven-repo")
// end::mavenBuildRepo[]

tasks.register("cleanMavenBuildRepo", Delete) {
    description "Clean intermediate Maven Repository '${mavenBuildRepo}'"
    delete mavenBuildRepo
}

task signAll() {
    doLast {
        logger.quiet("Signed all artifacts for upload with JReleaser")
    }
}
signAll.dependsOn(
        ':htmlSanityCheck-core:signMavenJavaPublication',
        ':htmlSanityCheck-gradle-plugin:signHtmlSanityCheckPluginMarkerMavenPublication',
        ':htmlSanityCheck-gradle-plugin:signPluginMavenPublication'
)

jreleaserDeploy.dependsOn(
        'signAll',
        ':htmlSanityCheck-core:publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository',
        ':htmlSanityCheck-gradle-plugin:publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository'
)

jreleaser {
    project {
        license = 'Apache-2.0'
        links {
            homepage = urls.website
        }
        inceptionYear = '2014'
        copyright = '2024'

        authors = ['Gernot Starke', 'Gerd Aschemann']
    }

    release {
        github {
            repoOwner = 'aim42'
            overwrite = true

            tagName = '{{projectVersion}}'

            changelog {
                external = 'CHANGELOG.md'
            }

            prerelease {
                enabled = true
                pattern = '.*-rc\\d+$'
            }
        }
    }

    deploy {
        active = 'NEVER'

        maven {
            mavenCentral {
                app {
                    active = 'ALWAYS'
                    sign = false
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository(mavenBuildRepo.toString())
                }
            }
        }
    }

    announce {
        active = 'ALWAYS'

        mastodon {
            active = 'ALWAYS'
            host = 'https://mastodon.social'

            statusTemplate = 'src/templates/jreleaser/mastodon.tpl'
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    apply plugin: 'jacoco'
    apply plugin: 'signing'

    description "${rootProject.description} - Module ${project.name}"

    dependencies {
        implementation platform(libs.slf4j.bom)

        testImplementation platform(libs.spock)
        testImplementation "org.spockframework:spock-core"
        testImplementation "org.spockframework:spock-junit4"

        testImplementation libs.junit.vintage
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
    }

    publishing {
        publications.all { publication ->
            if (publication instanceof MavenPublication) {
                publication.pom {
                    name = project.name
                    description = project.description
                    url = urls.website
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gstarke'
                            name = 'Gernot Starke'
                            email = 'gs@gernotstarke.de'
                        }
                        developer {
                            id = 'ascheman'
                            name = 'Gerd Aschemann'
                            email = 'gerd@aschemann.net'
                        }
                        developer {
                            id = 'rdmueller'
                            name = 'Ralf D. MÃ¼ller'
                            email = 'ralf.d.mueller@gmail.com'
                        }
                        developer {
                            id = 'ruhrotht'
                            name = 'Thomas Ruhroth'
                            email = 'Thomas.Ruhroth@msg.group'
                        }
                    }
                    scm {
                        connection = urls.connection
                        developerConnection = urls.developerConnection
                        url = urls.scm
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'myLocalRepositoryForFullIntegrationTests'
                url = mavenBuildRepo
            }
            mavenLocal()
        }
    }

    tasks.named('test', Test) {
        useJUnitPlatform()
    }

    check {
        finalizedBy jacocoTestReport // report is always generated after tests run
    }
    jacocoTestReport {
        reports {
            xml.required = true
        }
        dependsOn check // tests are required to run before generating the report
    }

    signing {
        required = { project.hasProperty('enableSigning') && project.property('enableSigning') == 'true' }
        if (project.hasProperty('useGpgCmd') && project.property('useGpgCmd') == 'true') {
            useGpgCmd()
        }
        sign publishing.publications
    }
}

def groovyVersion = GroovySystem.version
def groovyVersionMajorMinor = groovyVersion.split('\\.')[0..1].join('.')

tasks.named("dependencyUpdates").configure {
    gradleReleaseChannel = true

    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                if (selection.candidate.version =~ /(alpha|M)/) {
                    selection.reject('Rejected alpha or milestone version')
                } else if (selection.candidate.group == 'org.spockframework') {
                    def spockGroovyVersion = selection.candidate.version.split('-')[2]
                    if (spockGroovyVersion != groovyVersionMajorMinor) {
                        selection.reject("Spock's Groovy version ($spockGroovyVersion) does not match project's Groovy major/minor version ($groovyVersionMajorMinor)")
                    }
                } else if (selection.candidate.group == 'org.codehaus.groovy') {
                    if (selection.candidate.version != groovyVersion) {
                        selection.reject("Groovy version is determined by Gradle API (${groovyVersion})")
                    }
                }
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", System.getenv("SONAR_PROJECT_KEY") ?: "aim42_htmlSanityCheck"
        property "sonar.organization", System.getenv('SONAR_ORGANIZATION') ?: "aim42"
        property "sonar.host.url", System.getenv('SONAR_URL') ?: "https://sonarcloud.io"
        property "sonar.scm.provider", "git"
        property "sonar.coverage.jacoco.xmlReportPaths", "**/build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.buildbreaker.skip", "false"
        property "sonar.qualitygate.wait", true
    }
}

tasks.register("publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository") {
    group("Publishing")
    description("Publishes all publications to the local Maven repository")
}
publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository.configure {
    dependsOn(":htmlSanityCheck-core:publishMavenJavaPublicationToMyLocalRepositoryForFullIntegrationTestsRepository",
            ":htmlSanityCheck-gradle-plugin:publishPluginMavenPublicationToMyLocalRepositoryForFullIntegrationTestsRepository",
            ":htmlSanityCheck-gradle-plugin:publishHtmlSanityCheckPluginMarkerMavenPublicationToMyLocalRepositoryForFullIntegrationTestsRepository"
    )
}

final String INTEGRATION_TEST_DIRECTORY = "integration-test"
final String INTEGRATION_TEST_DIRECTORY_CLI = "${INTEGRATION_TEST_DIRECTORY}/cli"
final String INTEGRATION_TEST_DIRECTORY_COMMON = "${INTEGRATION_TEST_DIRECTORY}/common"
final String INTEGRATION_TEST_DIRECTORY_GRADLE_PLUGIN = "${INTEGRATION_TEST_DIRECTORY}/gradle-plugin"

tasks.register("integrationTestPrepare") {
    group("Verification")
    description("Prepare integration tests data")

    doLast {
        def result = exec {
            workingDir INTEGRATION_TEST_DIRECTORY_COMMON
            commandLine System.getProperty("os.name") ==~ /Windows.*/
                    ? "..\\..\\gradlew.bat"
                    : "../../gradlew", "clean", "buildReadmeAll"
        }
        logger.debug "Script output: $result"
    }
}
tasks.register("cleanIntegrationTestCommon", Delete) {
    description("Deletes the common data for integration tests")

    doLast {
        delete(file(INTEGRATION_TEST_DIRECTORY_COMMON))
    }
}
//clean.dependsOn(cleanIntegrationTestCommon)
integrationTestPrepare.mustRunAfter(cleanIntegrationTestCommon)

tasks.register("integrationTestGradlePluginOnly") {
    group("Verification")
    description("Run gradle-plugin integration tests (only)")

    final File testIndex = new File(file(INTEGRATION_TEST_DIRECTORY_GRADLE_PLUGIN), "${Project.DEFAULT_BUILD_DIR_NAME}/reports/index.html")
    outputs.file testIndex

    doLast {
        def result = exec {
            workingDir INTEGRATION_TEST_DIRECTORY_GRADLE_PLUGIN
            commandLine System.getProperty("os.name") ==~ /Windows.*/ ? "gradlew.bat" : "./gradlew", "clean", "htmlSanityCheck", "-PhtmlSanityCheckVersion=${project.version}"
        }
        logger.debug "Script output: $result"
        assert testIndex.exists()
    }
}
integrationTestGradlePluginOnly.configure {
    dependsOn(integrationTestPrepare)
    mustRunAfter('publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository')
}
tasks.register("integrationTestGradlePlugin") {
    group("Verification")
    description("Run overall gradle-plugin integration tests (and publish first)")
}
integrationTestGradlePlugin.dependsOn('publishAllPublicationsToMyLocalRepositoryForFullIntegrationTestsRepository',
        'integrationTestGradlePluginOnly')
tasks.register("cleanIntegrationTestGradlePlugin", Delete) {
    group("Build")
    description("Deletes the result directory from gradle-plugin integration tests")

    doLast {
        delete(file(INTEGRATION_TEST_DIRECTORY_GRADLE_PLUGIN))
    }
}

tasks.register("integrationTestCliOnly") {
    group("Verification")
    description("Run CLI integration tests (only)")

    final String BUILD_REPORTS_DIRECTORY = "${Project.DEFAULT_BUILD_DIR_NAME}/reports"
    final File testIndex = new File(file(INTEGRATION_TEST_DIRECTORY_CLI), "${BUILD_REPORTS_DIRECTORY}/index.html")

    outputs.file testIndex

    doLast {
        def result = exec {
            workingDir INTEGRATION_TEST_DIRECTORY_CLI
            file(BUILD_REPORTS_DIRECTORY).mkdirs()
            String hscScriptFileName = "../../htmlSanityCheck-cli/${Project.DEFAULT_BUILD_DIR_NAME}/install/hsc/bin/hsc"
            String params = "-r ${BUILD_REPORTS_DIRECTORY} ../common/${Project.DEFAULT_BUILD_DIR_NAME}/docs"
            if (System.getProperty("os.name") ==~ /Windows.*/) {
                commandLine 'cmd', '/c', "echo off && ${hscScriptFileName.replace('/', '\\')}.bat ${params}"
            } else {
                commandLine 'sh', '-c', "${hscScriptFileName} ${params}"
            }
        }
        logger.debug "Script output: ${result}"
        assert testIndex.exists()
    }
}
integrationTestCliOnly.configure {
    dependsOn(integrationTestPrepare)
    mustRunAfter(':htmlSanityCheck-cli:installDist')
}
tasks.register("integrationTestCli") {
    group("Verification")
    description("Run overall CLI integration tests (and publish first)")
}
integrationTestCli.configure {
    dependsOn(':htmlSanityCheck-cli:installDist', 'integrationTestCliOnly')
}
tasks.register("cleanIntegrationTestCli", Delete) {
    group("Build")
    description("Deletes the result directory from CLI integration tests")

    doLast {
        delete(file(INTEGRATION_TEST_DIRECTORY_CLI))
    }
}
tasks.register("integrationTestOnly") {
    group("Verification")
    description("Run overall integration tests (only)")
}
integrationTestOnly.dependsOn(
        'integrationTestGradlePluginOnly',
        'integrationTestCliOnly'
)

tasks.register("integrationTest") {
    group("Verification")
    description("Run overall integration tests")
}
integrationTest.dependsOn(
        'integrationTestGradlePlugin',
        'integrationTestCli'
)

/*
 * Copyright Gernot Starke and aim42 contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 */
