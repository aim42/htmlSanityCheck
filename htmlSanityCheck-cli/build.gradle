plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.fussionlabs.gradle.docker-plugin' version '0.6.0'

}

dependencies {
    implementation libs.picocli.impl
    annotationProcessor libs.picocli.annotationprocessor

    implementation platform("org.codehaus.groovy:groovy-bom:${GroovySystem.version}")
    implementation 'org.codehaus.groovy:groovy'
    implementation 'org.slf4j:slf4j-jdk14'

    implementation project(":htmlSanityCheck-core")

    testImplementation libs.mockito
}

compileGroovy {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

application {
    mainClass = 'org.aim42.htmlsanitycheck.cli.HscCommand'
    applicationName = 'hsc'
}

shadowJar {
    archiveClassifier.set('all')
    mergeServiceFiles()
    append 'META-INF/services'
}

def dockerTags(Project project) {
    String currentBranch = "${'git branch --show-current'.execute().text.trim()}"
    def result = [
            "${currentBranch == 'main' ? 'latest' : java.time.LocalDateTime.now().format(java.time.format.DateTimeFormatter.ofPattern('yyyyMMddHHmmss'))}" as String,
            "${currentBranch.replaceAll('/', '-')}" as String,
            ]
    if (currentBranch == 'main') {
        // On the main branch we should have the latest major prepended with 'v' for GH actions
        result += [ 'v' + project.version.substring(0, 1) ]
    }

    return result
}

docker {
    registry = "ghcr.io"
    repository = "aim42/hsc"
    platforms = ["linux/amd64", "linux/arm64"]
    buildArgs = [VERSION: project.version]
    dockerFilePath = "."
    tags = dockerTags(project)
}

/*
 * Copyright Gerd Aschemann and aim42 contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 */